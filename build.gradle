plugins {
	id 'java'
	id 'eclipse'
	id 'jacoco'
	id "maven-publish"
	id "signing"
	id "de.undercouch.download" version "5.6.0"
	id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

project.group = 'de.hhu.stups'
project.version = "1.2.3-SNAPSHOT"
final isSnapshot = project.version.endsWith("-SNAPSHOT")

final snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
final releasesStagingRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/"
repositories {
	mavenCentral()
	if (isSnapshot) {
		maven {
			name = "Sonatype snapshots"
			url = snapshotsRepoUrl
		}
	}
}

configurations.all {
	resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

def parser_version = "2.15.2"

dependencies {
	// The upstream version of the TLA+ tools is only published to Sonatype Snapshots, not to Maven Central.
	// Even the snapshot is currently (2026-01) unavailable since Sonatype shut down the old OSSRH service,
	// because the TLA+ people haven't updated their snapshot upload for the new Maven Central Portal yet.
	// See:
	// https://github.com/tlaplus/tlaplus/issues/1220
	// https://github.com/tlaplus/tlaplus/issues/1247
	//implementation(group: 'com.lamport', name: 'tla2tools', version: '1.8.0-SNAPSHOT')

	implementation(group: 'commons-cli', name: 'commons-cli', version: '1.9.0')
	implementation(group: 'de.hhu.stups', name: 'tlatools', version: '1.1.0')

	implementation(group: 'de.hhu.stups', name: 'bparser', version: parser_version)
	implementation(group: 'de.hhu.stups', name: 'ltlparser', version: parser_version)

	testImplementation(platform(group: 'org.junit', name: 'junit-bom', version: '5.13.2'))
	testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter')
	testImplementation(group: 'org.junit.jupiter', name: 'junit-jupiter-params')
	testCompileOnly(group: 'junit', name: 'junit', version: '4.13.2')
	testRuntimeOnly(group: 'org.junit.vintage', name: 'junit-vintage-engine')
	testRuntimeOnly(group: 'org.junit.platform', name: 'junit-platform-launcher')
	testImplementation(group: 'de.hhu.stups', name: 'tla2bAST', version: '1.4.2')
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8

	withSourcesJar()
	withJavadocJar()
}

javadoc {
	// silence warnings on missing javadoc
	options.addBooleanOption('Xdoclint:all,-missing', true)
	if (JavaVersion.current().isJava9Compatible()) {
		options.addBooleanOption('html5', true)
	}
}

test {
	useJUnitPlatform()

	exclude('de/tlc4b/tlc/integration/probprivate')
	exclude('testing')
	//exclude('de/tlc4b/tlc/integration')
}

jacoco {
	toolVersion = "0.8.13"
}

jacocoTestReport {
	reports {
		xml.required = false
		csv.required = false
		html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
	}
}

task downloadPublicExamples(type: Download) {
	src 'https://stups.hhu-hosting.de/downloads/prob/source/ProB_public_examples.tgz'
	dest buildDir
	onlyIfModified true
}

task extractPublicExamples(dependsOn: downloadPublicExamples, type: Copy) {
	final outDir = "${buildDir}/prob_examples"
	doFirst {
		delete(outDir)
	}
	
	from tarTree(resources.gzip("${buildDir}/ProB_public_examples.tgz"))
	into outDir
	include "public_examples/TLC/**"
}

clean {
	delete "${projectDir}/public_examples" // now extracted into build, but previous versions placed it at top level
	delete "${projectDir}/states"
	delete "${projectDir}/temp"
}

task regressionTests(dependsOn: extractPublicExamples, type: Test) {
	testClassesDirs = testing.suites.test.sources.output.classesDirs
	classpath = testing.suites.test.sources.runtimeClasspath

	include('de/tlc4b/tlc/integration/probprivate/**')
}
check.dependsOn(regressionTests)

// type 'gradle integrationTests jacocoIntegrationTestReport' in order to run the jacoco code coverage analysis
task jacocoIntegrationTestReport(type: JacocoReport) {
	sourceSets sourceSets.main
	//executionData files('build/jacoco/integrationTests.exec')
	executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = "TLC integration into ProB"
				description = "Use the TLC model checker within ProB."
				url = "https://github.com/hhu-stups/tlc4b"

				licenses {
					license {
						name = "Eclipse Public License, Version 2.1"
						url = "https://www.eclipse.org/legal/epl-v10.html"
					}
				}

				scm {
					connection = "scm:git:git://github.com/hhu-stups/tlc4b.git"
					developerConnection = "scm:git:git@github.com:hhu-stups/tlc4b.git"
					url = "https://github.com/bendisposto/hhu-stups/tlc4b"
				}

				developers {
					developer {
						id = "bendisposto"
						name = "Jens Bendisposto"
						email = "jens@bendisposto.de"
					}
				}
			}
		}
	}
}

nexusPublishing {
	repositories {
		mavenCentral {
			snapshotRepositoryUrl.set(uri(snapshotsRepoUrl))
			nexusUrl.set(uri(releasesStagingRepoUrl))
		}
	}
}

ext."signing.secretKeyRingFile" = rootProject.file("secring.gpg").absolutePath

signing {
	sign publishing.publications.mavenJava
}
